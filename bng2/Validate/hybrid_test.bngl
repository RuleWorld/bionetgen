# Hybrid Generator Test: 
#   Trivalent ligand - bivalent receptor model 

begin model
begin parameters
	## Peaking Trajectory
	Lig_tot   50000
	Rec_tot   3000
	cTot      2.7
	beta      16.8
	koff      0.01
	kp1       (cTot*koff)/(3.0*Lig_tot)  # FREE BINDING RATE
	kp2       (beta*koff)/Rec_tot        # CROSSLINKING RATE
    k_gather  100000
end parameters
begin molecule types
    R(l,l)
    L(r,r,r)
end molecule types
begin seed species
    R(l,l)     Rec_tot
    L(r,r,r)   Lig_tot
end seed species
begin observables
	Molecules  Rfree    R(l,l)
	Molecules  Lfree    L(r,r,r)
	Molecules  Rtot     R()
	Molecules  Ltot     L()
	Molecules  LRmotif  L(r!0).R(l!0)
	Species    R2       R==2
	Species    R3       R==3
	Species    R4       R==4
    Species    R5       R>4
end observables
begin reaction rules
	R(l!1).L(r!1)          ->  R(l)  +  L(r)          koff
	L(r,r,r)      +  R(l)  ->  L(r!1,r,r).R(l!1)      kp1
	L(r,r,r!+)    +  R(l)  ->  L(r!1,r,r!+).R(l!1)    kp2
	L(r,r!+,r!+)  +  R(l)  ->  L(r!1,r!+,r!+).R(l!1)  kp2
end reaction rules
begin population maps
    L(r,r,r)  ->  L_pop()    k_gather
    R(l,l)    ->  R_pop()    k_gather
end population maps
end model

## actions ##
generate_hybrid_model({overwrite=>1, execute=>1, verbose=>1, actions=>["writeXML()"]})

