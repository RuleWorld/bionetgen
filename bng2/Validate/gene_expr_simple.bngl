# Simple of Gene Expression Model
# from: V Shahrezaei and PS Swain (2008) PNAS
# doi: 10.1073/pnas.0803850105
#
#  NOTE: This model tests the fixed species flag in SSA simulations.
#
# Let n be the number of proteins in the system at equilibrium.
# In the limit of gamma -> infinity, we have:
#   P(n) = G(a+n)/(G(n+1)*G(a)) * (b/(1+b))^n * (1 - b/(1+b))^a
# where G is the Gamma function and a, b, and gamma are unitless parameters
# related to the kinetic parameters (see parameter block below)
#
# We can validate the equilibrium distribution of our simulator by collecting N=10,000 samples,
#  placing those samples into B=19 bins, and comparing the expected and observed frequencies
#  using the Pearson's chi-square test with D=18 degrees of freedom.
#
# Bins: n = 0, 1, 2, 3-4, 5-6, 7-9, 10-13, 14-17, 18-22, 23-27, 28-34, 35-43, 44-53,
#            54-65, 66-81, 82-103, 104-135, 136-192, 193-Inf
#
# p(Bins):  0.0995037190209989, 0.0492592668420787, 0.0365786634965931, 0.0563268149291556, 0.0444445218489067,
#           0.0543605006589945, 0.0586549736271397, 0.0486069790492784, 0.051242820575514,  0.0436300426658215,
#           0.0517608234772298, 0.0548616157992881, 0.0497506759070256, 0.0483489170078124, 0.0505360343329532,
#           0.0513894123055027, 0.0504918139972594, 0.0500851526255915, 0.0501672518328555
#
# chi-square calculation:  SUM_b=1..B[ (Observed(b) - N*pBin(b))^2 / (N*pBin(b)) ]
#
# p-values: 0.200 => 22.760
#           0.100 => 25.989
#           0.050 => 28.869
#           0.020 => 32.346
#           0.010 => 34.805
#           0.005 => 37.156
#           0.002 => 40.136
#           0.001 => 42.312
# (degrees of freedom = 18)
# reference: http://www.medcalc.org/manual/chi-square-table.php

begin model
begin parameters
    # unitless parameters
    a      0.5         # a := v0/d1
    b      100         # b := v1/d0
    gamma  10          # gamma := d0/d1
    # fundamental kinetic parameters
    d1     0.0005      # /s
    # derived kinetic parameters
    d0     gamma*d1    # /s
    v0     a*d1        # /s
    v1     b*gamma*d1  # /s
end parameters
begin molecule types
    mRNA()
    Protein()
end molecule types
begin seed species
    mRNA()     0
    Protein()  0
end seed species
begin observables
    Molecules  mRNA_Total     mRNA()
    Molecules  Protein_Total  Protein()
end observables
begin reaction rules
    # mRNA synthesis
    0 -> mRNA()  v0
    # Protein synthesis
    mRNA()  ->  mRNA() + Protein()  v1
    # degradation
    mRNA()    -> 0  d0
    Protein() -> 0  d1
end reaction rules
end model

## actions ##
generate_network({overwrite=>1})
simulate_ssa({suffix=>"burnin",t_start=>0,t_end=>10000000,n_steps=>1})
saveConcentrations()
simulate_ssa({suffix=>"ssa_equil",t_start=>0,t_end=>200000000,n_steps=>10000})

