# Simple model tracking fluorescent labels
begin parameters
NA 6.02e23 # Avogadro's number (molecues/mol)
f  0.1       # Fraction of the cell to simulate
Vo f*1.0e-10 # Extracellular volume=1/cell_density (L)
V  f*3.0e-12 # Cytoplasmic volume (L)
# Initial concentrations (copies per cell)
A_tot 10000  
B_tot  8000 
D_tot 50000
# Rate constants 
# Divide by NA*Vcyt to convert bimolecular rate constants
# from /M/sec to /(molecule/cell)/sec
kpAB 3.0e6/(NA*V) 
kmAB 0.06         
kpCD 1.0e6/(NA*V) 
kmCD 0.06         
kpI  1.0e7/(NA*V) 
kmI   0.1
end parameters
begin molecule types
A(f~off~on)
B()
C(f~off~on)
D()
E(f~off~on)
I()
end  molecule types
begin species
A(f~off) A_tot
B()	 B_tot
C(f~off) 0
D()	 D_tot
E(f~off) 0
I()      0
end species
begin observables
Molecules A_f A(f~on)
Molecules C_f C(f~on)
Molecules E_f E(f~on)
Molecules Tot_f A(f~on),C(f~on),E(f~on)
end observables
begin reaction rules
A(f%t1) + B() <-> C(f%t1) kpAB, kmAB
C(f%t1) + D() <-> E(f%t1) kpCD, kmCD
A(f~off) + I <-> A(f~on) kpI, kmI
end reaction rules

generate_network({overwrite=>1});
writeXML({});
# Equilibrate
simulate_ode({suffix=>"equil",t_end=>10000,n_steps=>10,steady_state=>1});
# Add indicator
setConcentration("I","A_tot/10");
simulate_ode({t_end=>200,n_steps=>50});