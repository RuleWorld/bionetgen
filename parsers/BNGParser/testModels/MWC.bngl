begin parameters
k_RT_transition 1e-1
k_TR_transition 1e-1
k_f_R 1
k_r_R 100000
k_f_T 10
k_r_T 100000
R0 1000
L0 10000
end parameters

begin molecule types
M1: Protein(b,b,Conf~R~T)
M2: Ligand(c)
end molecule types

begin seed species
R_0: Protein(b,b,Conf~R) R0
Free_Ligand: Ligand(c) L0
end seed species

begin reaction rules
Rule_Transition: Protein(b,b,Conf~R)<->Protein(b,b,Conf~T) k_RT_transition,k_TR_transition 
Rule_Ligand_Binding_for_R: Protein(b,Conf~R)+Ligand(c)<->Protein(b!0,Conf~R).Ligand(c!0) k_f_R,k_r_R 
Rule_Ligand_Binding_for_T: Protein(b,Conf~T)+Ligand(c)<->Protein(b!0,Conf~T).Ligand(c!0) k_f_T,k_r_T 
end reaction rules

begin observables
Molecules BoundLigand Ligand(c!+) 
Species FreeProtein Protein(b,b) 
Species SingleBound Protein(b!0,b).Ligand(c!0) 
Species DoubleBound Protein(b!0,b!1).Ligand(c!0).Ligand(c!1) 
end observables

generate_network({overwrite=>1,check_iso=>1,max_iter=>25,max_stoich=>{Protein=>unlimited,Ligand=>unlimited}});

setConcentration("Ligand(c)",0);
simulate_ode({suffix=>"equil",t_end=>10000,n_steps=>100,atol=>1e-12,rtol=>1e-12,sparse=>1,steady_state=>1});

setConcentration("Ligand(c)","L0");

simulate_ode({t_end=>100,n_steps=>10000,atol=>1e-12,rtol=>1e-12,sparse=>0});

